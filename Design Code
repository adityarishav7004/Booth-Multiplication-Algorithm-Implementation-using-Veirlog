// Verilog Design Module 

module booth_multiplier #(
    parameter N = 8 // for number of bits
)(
    input  signed [N-1:0] multiplicand,
    input  signed [N-1:0] multiplier,
    output reg signed [2*N-1:0] product
);

    integer i;

    reg signed [2*N:0] P;   // {A, Q, Q-1}
    reg signed [2*N:0] M;   // multiplicand aligned with A

    always @(*) begin
       
        P = { {N{1'b0}}, multiplier, 1'b0 };
        M = { multiplicand, {(N+1){1'b0}} };

        for (i = 0; i < N; i = i + 1) begin
            case (P[1:0])
                2'b01: P = P + M;
                2'b10: P = P - M;
                default: P = P;
            endcase

            P = P >>> 1;
        end

        product = P[2*N:1];
    end

endmodule
